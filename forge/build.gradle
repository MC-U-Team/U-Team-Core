plugins {
	id "net.minecraftforge.gradle"
	id "org.parchmentmc.librarian.forgegradle"
	id "org.spongepowered.mixin"
	id "info.u_team.gradle_files"
	id "info.u_team.curse_gradle_uploader"
	id "maven-publish"
}

gradlefiles.loaderSuffix = "forge"

group = rootProject.group
base.archivesName = gradlefiles.archivesName()
version = gradlefiles.version()

sourceSets {
	main {
		java {
			srcDirs += "src/api/java"
			srcDirs += gradlefiles.project("common").sourceSets.common.java.srcDirs
		}
		resources {
			srcDirs += gradlefiles.project("common").sourceSets.common.resources.srcDirs
		}
	}
	test_mod {
		java {
			srcDirs += "src/data_test_mod/java"
			srcDirs += "src/integration_test_mod/java"
		}
		resources {
			srcDirs += "src/generated_test_mod/resources"
		}
		compileClasspath += main.runtimeClasspath
		runtimeClasspath += main.runtimeClasspath
	}
	multiloader_test_mod {
		java {
			srcDirs += gradlefiles.project("common").sourceSets."common-multiloader_test_mod".java.srcDirs
		}
		resources {
			srcDirs += gradlefiles.project("common").sourceSets."common-multiloader_test_mod".resources.srcDirs
		}
		compileClasspath += main.runtimeClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
	withJavadocJar()
}

minecraft {
	mappings channel: config.forge.mapping_channel, version: config.forge.mapping
	
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	
	runs {
		client {
			workingDirectory file("run/client")
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
				
				uteamtest {
					source sourceSets.test_mod
				}
				
				uteamtest_multiloader {
					source sourceSets.multiloader_test_mod
				}
			}
		}
		server {
			workingDirectory file("run/server")
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
				
				uteamtest {
					source sourceSets.test_mod
				}
				
				uteamtest_multiloader {
					source sourceSets.multiloader_test_mod
				}
			}
		}
		data {
			workingDirectory file("run/data")
			
			args "--mod", "uteamtest"
			args "--all"
			args "--output", file("src/generated_test_mod/resources")
			args "--validate"
			args "--existing", file("src/test_mod/resources")
			args "--existing-mod", "uteamcore"
			
			property "coremod.uteamcore.dataprovider-set-indent", "coremod.uteamcore.dataprovider-set-indent.value"
			property "coremod.uteamcore.dataprovider-set-indent.value", "true"
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
				
				uteamtest {
					source sourceSets.test_mod
				}
			}
		}
	}
}

mixin {
	add sourceSets.main, "uteamcore.mixins.refmap.json"
	config "uteamcore.mixins.json"
	
	debug.verbose = true
	debug.export = true
}

tasks.register("testJar", Jar) {
	archiveClassifier = "test"
	
	from sourceSets.test_mod.output
}

tasks.register("testSourcesJar", Jar) {
	archiveClassifier = "testsources"
	
	from sourceSets.test_mod.allJava
}

tasks.register("multiLoaderTestJar", Jar) {
	archiveClassifier = "multiloader_test"
	
	from sourceSets.multiloader_test_mod.output
}

tasks.register("multiLoaderTestSourcesJar", Jar) {
	archiveClassifier = "multiloader_testsources"
	
	from sourceSets.multiloader_test_mod.allJava
}

reobf.create("testJar") {
	dependsOn tasks.named("reobfJar")
}

tasks.named("reobfTestJar").configure {
	libraries.from(tasks.named("reobfJar").get().input)
}

reobf.create("multiLoaderTestJar") {
	dependsOn tasks.named("reobfJar")
}

tasks.named("reobfMultiLoaderTestJar").configure {
	libraries.from(tasks.named("reobfJar").get().input)
}

// Skip mixin gradle for our test jars
tasks.withType(org.spongepowered.asm.gradle.plugins.MixinExtension$AddMixinsToJarTask).configureEach { task ->
	if(task.name.endsWith("TestJar") || task.name.endsWith("TestSourcesJar")) {
		task.enabled = false
	}
}

tasks.withType(org.spongepowered.asm.gradle.plugins.MixinExtension$ConfigureReobfTask).configureEach { task ->
	if(task.name.endsWith("TestJar")) {
		task.enabled = false
	}
}

gradlefiles.assembleDependOn(tasks.named("reobfTestJar"), tasks.named("testSourcesJar"))
gradlefiles.allPublishingDependOn(tasks.named("reobfTestJar"), tasks.named("testSourcesJar"))
gradlefiles.assembleDependOn(tasks.named("reobfMultiLoaderTestJar"), tasks.named("multiLoaderTestSourcesJar"))
gradlefiles.allPublishingDependOn(tasks.named("reobfMultiLoaderTestJar"), tasks.named("multiLoaderTestSourcesJar"))

tasks.withType(Jar).configureEach { task ->
	gradlefiles.defaultJar(task)
}

tasks.withType(Javadoc).configureEach { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

signjar.sign(tasks.named("reobfJar"))
signjar.sign(tasks.named("reobfTestJar"))
signjar.sign(tasks.named("reobfMultiLoaderTestJar"))

processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
			file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
		}
	}
}

repositories {
	maven {
		url = "https://maven.blamejared.com"
	}
}

dependencies {
	minecraft gradlefiles.forgeDependency()
	
	//compileOnly fg.deobf("mezz.jei:jei-${config.minecraft.version}-forge-api:${config.jei.version}")
	//compileOnly fg.deobf("mezz.jei:jei-${config.minecraft.version}-common-api:${config.jei.version}")
	//runtimeOnly fg.deobf("mezz.jei:jei-${config.minecraft.version}-forge:${config.jei.version}")
	
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

curseforge {
	apiKey = defaultPropertyValue("curseforge.token")
	
	project {
		id = config.curse.id
		releaseType = config.curse.type
		
		addGameVersion config.minecraft.version
		addGameVersion "Forge"
		addGameVersion "Java 17"
		
		relations {
			optionalDependency "basic-discord-rich-presence"
			optionalDependency "jei"
		}
		
		mainArtifact(tasks.named("jar")) {
			displayName = "${config.curse.display}-Forge-${config.minecraft.version}-${project.version}"
			changelogType = "html"
			changelog = "<a href=\"${gradlefiles.changelogUrl()}\">Changelog of ${project.version}</a>"
		}
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId project.base.archivesName.get()
			
			from gradlefiles.filteredJavaComponent()
			artifact tasks.named("testJar")
			artifact tasks.named("testSourcesJar")
			artifact tasks.named("multiLoaderTestJar")
			artifact tasks.named("multiLoaderTestSourcesJar")
			
			pom {
				name = project.base.archivesName.get()
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "MrTroble"
					}
				}
			}
		}
	}
}

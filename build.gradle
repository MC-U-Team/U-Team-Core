buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
		maven { url = "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.3.0"
    }
}

apply from: "https://raw.githubusercontent.com/HyCraftHD/Blank-Forge-Project/master/configuration.methods"
apply from: "https://raw.githubusercontent.com/HyCraftHD/Blank-Forge-Project/master/git.methods"

setup()
info()

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven"

getBuildNumber()

group = "info.u-team"
archivesBaseName = "${config.mod.filename}-${config.forge.mcversion}"
version = "${config.mod.version}.${config.buildnumber}" + getAppendix();

def getAppendix() {
	if("${config.mod.snapshot}".toBoolean()) {
		return "-SNAPSHOT"
	} else {
		return ""
	}
}

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    mappings channel: config.forge.mapping_channel, version: config.forge.mapping
    accessTransformer = file("$rootDir/src/main/resources/META-INF/accesstransformer.cfg")
   
    runs {
        client = {
            workingDirectory project.file("run/client").canonicalPath
			mods {
                uteamcore {
                    source sourceSets.main
                }
				
				uteamtest {
                    source sourceSets.test
                }
            }
        }
        server = {
            workingDirectory project.file("run/server").canonicalPath
            mods {
                uteamcore {
                    source sourceSets.main
                }
				
				uteamtest {
                    source sourceSets.test
                }
            }
        }
        data = {
            workingDirectory project.file("run/data").canonicalPath
            args "--mod", "uteamcore", "--all", "--output", "\"" + file("src/generated/resources").toString() + "\""
            mods {
                uteamcore {
                    source sourceSets.main
				}
			}
        }
        data_test = {
        	parents data
            workingDirectory project.file("run/data").canonicalPath
            args "--mod", "uteamtest", "--all", "--output", "\"" + file("src/generated_test/resources").toString() + "\"", "--validate", "--existing", "\"" + sourceSets.test.resources.srcDirs[0] + "\""
            mods {
                uteamtest {
                    source sourceSets.test
				}
			}
        }
    }
}

def normalManifest = {
	attributes(
		"Specification-Title": config.mod.name,
        "Specification-Vendor": "Team UTeam",
        "Specification-Version": config.forge.major_version,
		"Built-On": config.forge.mcversion,
		"Implementation-Title": config.mod.name,
    	"Implementation-Version": project.version,
		"Implementation-Vendor": "Team UTeam",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
	)
}

jar {
	classifier = ""

    from "LICENSE"
    from "LICENSE-DEPENDENCIES"

   	manifest normalManifest
}

task deobfJar(type: Jar) {
	classifier = "dev"

    from sourceSets.main.output
    from "LICENSE"
    from "LICENSE-DEPENDENCIES"
        
    manifest normalManifest
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    
    from sourceSets.main.allJava
    from "LICENSE"
    from "LICENSE-DEPENDENCIES"
}

task docJar(type: Jar) {
    classifier = "javadoc"
    
    from javadoc
    from "LICENSE"
    from "LICENSE-DEPENDENCIES"
    
    tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.addStringOption('encoding', 'UTF-8')
		options.addStringOption('charSet', 'UTF-8')
	}
}

task testJar(type: Jar) {
	classifier = "test"

    from sourceSets.test.output
    from "LICENSE"
               
   	afterEvaluate {
	    reobf.create("testJar");
	    reobfTestJar.dependsOn createMcpToSrg
	    reobfTestJar.setMappings(createMcpToSrg.getOutput())
	    reobfTestJar.setClasspath(reobfTestJar.getClasspath().plus(files(jar)))   
	}
                
    manifest normalManifest
}

task testDeobfJar(type: Jar) {
	classifier = "testdev"

    from sourceSets.test.output
    from "LICENSE"
                      
    manifest normalManifest
}

task testSourcesJar(type: Jar) {
    classifier = "testsources"
    
    from sourceSets.test.allJava
    from "LICENSE"
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives docJar
    archives jar
    archives testJar
    archives testDeobfJar
    archives testSourcesJar
}

sourceSets {
   	main {
		java {
			srcDirs += "src/repack/java"
		}
		resources {
			srcDirs += "src/repack/resources"
			srcDirs += "src/generated/resources"
		}
   	}
   	test {
		java
		resources {
			srcDirs += "src/generated_test/resources"
		}
   	}
}

dependencies {
    minecraft "net.minecraftforge:forge:$config.forge.version"
}

compileJava {
    options.encoding = "UTF-8"
}

def generateChangelogUrl() {
	String url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/CHANGELOG.md"
	String mcversion = "${config.forge.mcversion}".replace(".", "")
	String version = "${project.version}".replace(".", "")
	String date =new Date().format("yyyy-MM-dd");
	return url + "#" + mcversion + "-" + version + "---" + date
}

curseforge {
	if(System.getenv("token") != null) {
		apiKey = System.getenv("token")
	} else {
		if (project.hasProperty("token")) {
			apiKey = project.getProperty("token")
		} else {
			apiKey = "mrTroble" //We dont have one lol ¯\_(ツ)_/¯
		}
	}
	
	tasks.curseforge.dependsOn build
	
  	project {
    	id = config.curse.id
    	releaseType = config.curse.type

		addGameVersion config.forge.mcversion
		
		options {
		    forgeGradleIntegration = false
		}

    	mainArtifact(jar) {
      		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}"
      		changelogType = "html"
      		changelog = "<a href=\"" + generateChangelogUrl() + "\">Changelog of ${project.version}</a>"
    	}
    	
    	addArtifact(deobfJar) {
    		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}-dev"
    		changelogType = "text"
    		changelog = "Mappings: ${config.forge.mapping_channel}_${config.forge.mapping}"
    	}
  	}
}

uploadArchives {

	tasks.uploadArchives.dependsOn build

	repositories {
		mavenDeployer {
			String mavenPass = System.getenv("mavenpass")
			if(mavenPass == null) {
				mavenPass = project.getProperty("mavenpass")
			}
			if (mavenPass != null) {
				repository(url: "https://repo.u-team.info") {
					authentication(userName: "maven", password: mavenPass)
				}
			}
			pom {
				groupId = project.group
				artifactId = project.archivesBaseName
				version = project.version
				project {
					name project.archivesBaseName
					packaging "jar"
					description "${config.github.name}"
						url "https://github.com/MC-U-Team/${config.github.name}"
					scm {
						url "https://github.com/MC-U-Team/${config.github.name}"
						connection "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
						developerConnection "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
					}
					issueManagement {
						system "github"
						url "https://github.com/MC-U-Team/${config.github.name}/issues"
					}
					licenses {
						license {
							name "Apache License 2.0"
							url "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
							distribution "repo"
						}
					}

					developers {
						developer {
							name "HyCraftHD"
						}
						developer {
							name "MrTroble"
						}
					}
				}
			}
		}
	}
}


task uploadBoth {
	tasks.uploadBoth.dependsOn build
	tasks.uploadBoth.dependsOn uploadArchives
	tasks.uploadBoth.dependsOn "curseforge${config.curse.id}"
	tasks.uploadBoth.dependsOn setBuildNumber
	doLast {
		println "Published both"
	}
}
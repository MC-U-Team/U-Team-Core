buildscript {
	repositories {
		maven {
			url = "https://maven.minecraftforge.net"
		}
		maven {
			url = "https://maven.parchmentmc.org"
		}
		maven {
			url = "https://repo.u-team.info"
		}
		mavenCentral()
	}
	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
		classpath group: "org.parchmentmc", name: "librarian", version: "1.+", changing: true
		classpath group: "info.u-team.curse_gradle_uploader", name: "curse_gradle_uploader", version: "1.4.1"
	}
}

apply from: "gradle_files/default-project-fg-5.groovy"

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.parchmentmc.librarian.forgegradle"
apply plugin: "eclipse"
apply plugin: "info.u_team.curse_gradle_uploader"
apply plugin: "maven-publish"

group = "info.u-team"
archivesBaseName = generateArchivesBaseName()
version = generateVersion()

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

minecraft {
	mappings channel: config.forge.mapping_channel, version: config.forge.mapping
	
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	
	runs {
		client = {
			workingDirectory file("run/client")
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
				
				uteamtest {
					source sourceSets.test
				}
			}
		}
		server = {
			workingDirectory file("run/server")
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
				
				uteamtest {
					source sourceSets.test
				}
			}
		}
		data = {
			workingDirectory file("run/data")
			
			args "--mod", config.mod.modid
			args "--all"
			args "--output", file("src/generated/resources")
			args "--validate"
			args "--existing", file("src/main/resources")
			
			mods {
				"${config.mod.modid}" {
					source sourceSets.main
				}
			}
		}
		data_test = {
			workingDirectory file("run/data")
			
			parent data // Does not work as the other args are added too...
			
			args "--mod", "uteamtest"
			args "--all"
			args "--output", file("src/generated_test/resources")
			args "--validate"
			args "--existing", file("src/test/resources")
			args "--existing-mod", "uteamcore"
			
			mods {
				uteamtest {
					source sourceSets.test
				}
			}
		}
	}
}

jar {
	archiveClassifier = ""
	
	from "LICENSE"
	
	exclude ".cache"
	
	manifest normalManifest
}

task deobfJar(type: Jar) {
	archiveClassifier = "dev"
	
	from sourceSets.main.output
	from "LICENSE"
	
	exclude ".cache"
	
	manifest normalManifest
}

task sourcesJar(type: Jar) {
	archiveClassifier = "sources"
	
	from sourceSets.main.allJava
	from "LICENSE"
}

task docJar(type: Jar) {
	archiveClassifier = "javadoc"
	
	from javadoc
	from "LICENSE"
	
	tasks.withType(Javadoc) {
		options.addStringOption("Xdoclint:none", "-quiet")
		options.addStringOption("encoding", "UTF-8")
		options.addStringOption("charSet", "UTF-8")
	}
}

task testJar(type: Jar) {
	archiveClassifier = "test"
	
	from sourceSets.test.output
	from "LICENSE"
	
	exclude ".cache"
	
	afterEvaluate {
		reobf.create("testJar");
		reobfTestJar.dependsOn createMcpToSrg
		reobfTestJar.setMappings(createMcpToSrg.getOutput())
		reobfTestJar.getClasspath().plus(files(jar))
	}
	
	manifest normalManifest
}

task testDeobfJar(type: Jar) {
	archiveClassifier = "testdev"
	
	from sourceSets.test.output
	from "LICENSE"
	
	exclude ".cache"
	
	manifest normalManifest
}

task testSourcesJar(type: Jar) {
	archiveClassifier = "testsources"
	
	from sourceSets.test.allJava
	from "LICENSE"
}

artifacts {
	archives jar
	archives deobfJar
	archives sourcesJar
	archives docJar
	archives testJar
	archives testDeobfJar
	archives testSourcesJar
}

signAllJars()

sourceSets {
	main {
		java {
			srcDirs += "src/data/java"
			srcDirs += "src/api/java"
			srcDirs += "src/integration/java"
		}
		resources {
			srcDirs += "src/generated/resources"
		}
	}
	test {
		java {
			srcDirs += "src/data_test/java"
			srcDirs += "src/integration_test/java"
		}
		resources {
			srcDirs += "src/generated_test/resources"
		}
	}
}

repositories {
	maven {
		url = "https://dvs1.progwml6.com/files/maven"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.18.1-39.0.5"
	compileOnly fg.deobf("mezz.jei:jei-1.18:9.0.0.40:api")
	runtimeOnly fg.deobf("mezz.jei:jei-1.18:9.0.0.40")
}

curseforge {
	afterEvaluate {
		tasks."curseforge${config.curse.id}".onlyIf {
			getValue("token") != null
		}
	}
	
	tasks.curseforge.dependsOn build
	
	apiKey = getValueDefault("token")
	
	project {
		id = config.curse.id
		releaseType = config.curse.type
		
		addGameVersion config.forge.mcversion
		addGameVersion "Forge"
		addGameVersion "Java 17"
		
		relations {
			optionalDependency "basic-discord-rich-presence"
			optionalDependency "jei"
		}
		
		mainArtifact(jar) {
			displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}"
			changelogType = "html"
			changelog = "<a href=\"${generateChangelogUrl()}\">Changelog of ${project.version}</a>"
		}
		
		addArtifact(deobfJar) {
			// TODO remove deobf maybe?
			displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}-dev (Use only for development)"
			changelogType = "text"
			changelog = "ONLY for development environment!\nMappings: ${config.forge.mapping_channel}_${config.forge.mapping}"
		}
	}
}

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		getValue("mavenpass") != null
	}
}

publishing {
	publish.dependsOn build
	
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = getValue("mavenpass")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			artifact deobfJar
			artifact sourcesJar
			artifact docJar
			artifact testJar
			artifact testDeobfJar
			artifact testSourcesJar
			
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			
			pom {
				name = project.archivesBaseName
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "MrTroble"
					}
				}
			}
		}
	}
}

task uploadBoth {
	tasks.uploadBoth.dependsOn build
	tasks.uploadBoth.dependsOn publish
	tasks.uploadBoth.dependsOn "curseforge${config.curse.id}"
	tasks.uploadBoth.dependsOn setBuildNumber
	tasks.uploadBoth.dependsOn tagRelease
	doLast {
		println "Published both"
	}
}

gradle.taskGraph.whenReady {
	if (gradle.taskGraph.hasTask(":curseforge${config.curse.id}") || gradle.taskGraph.hasTask(":publish")) {
		validateBuild()
	}
}

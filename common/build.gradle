plugins {
	id "fabric-loom"
	id "info.u_team.gradle_files"
	id "maven-publish"
}

gradlefiles.loaderSuffix = "common"

group = rootProject.group
base.archivesName = gradlefiles.archivesName()
version = gradlefiles.version()

sourceSets {
	common {
		java {
			srcDirs += "src/common_api/java"
			srcDirs += "src/common_integration/java"
		}
		resources {
			srcDirs += "src/common_generated/resources"
		}
	}
	main {
		java {
			srcDirs += sourceSets.common.java.srcDirs
		}
		resources {
			srcDirs += sourceSets.common.resources.srcDirs
		}
	}
	common_multiloader_test_mod {
		java
		resources {
			srcDirs += "src/common_generated_multiloader_test_mod/resources"
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
	withJavadocJar()
}

loom {
	accessWidenerPath = gradlefiles.project("fabric").file("src/main/resources/uteamcore.accesswidener")
}

tasks.withType(Jar).configureEach { task ->
	gradlefiles.defaultJar(task)
	
	manifest {
		attributes("Fabric-Loom-Remap": "true")
	}
}

tasks.withType(Javadoc).configureEach { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

signjar.sign(tasks.named("remapJar"))

processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
			file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
		}
	}
}

repositories {
	maven {
		url = "https://maven.parchmentmc.org"
	}
	maven {
		url = "https://maven.blamejared.com"
	}
}

dependencies {
	minecraft gradlefiles.fabricMinecraftDependency()
	mappings loom.layered() {
		officialMojangMappings()
		parchment(config.fabric.mapping_layer)
	}
	
	modImplementation gradlefiles.fabricLoaderDependency()
	modRuntimeOnly gradlefiles.fabricApiDependency()
	
	modCompileOnly "mezz.jei:jei-${config.minecraft.version}-common-api:${config.jei.version}"
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId project.base.archivesName.get()
			
			from gradlefiles.filteredJavaComponent()
			
			pom {
				name = project.base.archivesName.get()
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "MrTroble"
					}
				}
			}
		}
	}
}
